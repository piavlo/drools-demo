package com.zohararad.rules

import com.zohararad.GenericEvent;
import com.zohararad.EventCounter;
import java.util.Date;

dialect "mvel"

declare GenericEvent
  @role( event )
  //@expires( 1m )
end

declare EventCounter
  @role( event )
end

rule "count views"
  when
    $views : EventCounter( count > 0 ) from accumulate(
               GenericEvent( $event : event == "view", $id : id ) from entry-point EventsStream,
               init( EventCounter ec = new EventCounter(); ),
               action( ec.addEvent( $event, $id ); ),
               reverse( ec.removeEvent( $event, $id ); ),
               result( ec )
             )
    $conversions : EventCounter( count > 4, id == $views.id ) from accumulate(
               GenericEvent( $event : event == "conversion", $id : id == $views.id ) over window:time(1m) from entry-point EventsStream,
               init( EventCounter ec = new EventCounter(); ),
               action( ec.addEvent( $event, $id ); ),
               reverse( ec.removeEvent( $event, $id ); ),
               result( ec )
             )
  then
    System.out.println($conversions.event + ": " + $conversions.id + " - "+ $conversions.count + ". Fire rules");
end